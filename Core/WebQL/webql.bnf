<digit> ::= "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9"

<letter> ::= "a" | "b" | "c" | "d" | "e" | "f" | "g" | "h" | "i" | "j" | "k" | "l" | "m" | "n" | "o" | "p" | "q" | "r" | "s" | "t" | "u" | "v" | "w" | "x" | "y" | "z" | "A" | "B" | "C" | "D" | "E" | "F" | "G" | "H" | "I" | "J" | "K" | "L" | "M" | "N" | "O" | "P" | "Q" | "R" | "S" | "T" | "U" | "V" | "W" | "X" | "Y" | "Z"

<special_char> ::= " " | "!" | "\"" | "#" | "$" | "%" | "&" | "'" | "(" | ")" | "*" | "+" | "," | "-" | "." | "/" | ":" | ";" | "<" | "=" | ">" | "?" | "@" | "[" | "\\" | "]" | "^" | "_" | "`" | "{" | "|" | "}" | "~"

<ascii_char> ::= <letter> | <digit> | <special_char>

<char> ::= <ascii_char>

<null> ::= "null"
<bool> ::= "true" | "false"

<colon> ::= ":"
<dot> ::= "."
<dollar_sign> ::= "$"
<double_quote> ::= "\""
<underscore> ::= "_"
<comma> ::= ","

<integer> ::= <digit> | <integer> <digit>
<float> ::= <integer> <dot> <digit> | <float> <digit>
<number> ::= <integer> | <float>

<version> ::= <digit> <dot> <digit> <dot> <digit>

<identifier> ::= <letter> | <identifier> <letter> | <identifier> <digit> | <underscore> <identifier> | <identifier> <underscore>

<char_sequence> ::= <char> | <char_sequence> <char>
<string> ::= <double_quote> <double_quote> | <double_quote> <char_sequence> <double_quote>

<primitive_type> ::= <number> | <string> | <bool> | <null>

<array_element> ::= <primitive_type> | <expression> | <array_element> <comma> (<primitive_type> | <expression>)
<array> ::= "[" "]" | "[" <array_element> "]"

<equals_op> ::= <dollar_sign> "equals"
<not_op> ::= <dollar_sign> "not"
<like_op> ::= <dollar_sign> "like"
<operator> ::= <equals_op> | <not_op> | <like_op>

<lhs_term> ::= <identifier> | <operator>
<rhs_term> ::= <primitive_type> | <expression> | <array>

<empty_expression> ::= "{ }"
<expression_separator> ::= <colon>
<expression> ::= <empty_expression> | "{" <lhs_term> <expression_separator> <rhs_term> "}"

<empty_query> ::= "..."
<where> ::= "where" <expression_separator> <expression>
<project> ::= "project" <expression_separator> <expression>
<order> ::= "order" <expression_separator> <expression>

<query> ::= <empty_query> | "{" <where> "}" | "{" <where> <comma> <project> "}" | "{" <where> <comma> <project> <comma> <order> "}"